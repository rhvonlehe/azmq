cmake_minimum_required(VERSION 3.16...3.25)
project(test_azmq LANGUAGES CXX)

if (TEST_INSTALLED_VERSION)
    find_package(azmq 1.1 REQUIRED)
    enable_testing()
endif ()

# NOTE: current Catch2 version is 3.2.0 is not compatible! CK
find_package(Catch2 2.13 CONFIG QUIET)
if (NOT TARGET Catch2::Catch2)
    include(FetchContent)
    FetchContent_Declare(Catch2 GIT_REPOSITORY https://github.com/catchorg/Catch2.git GIT_TAG v2.13.7)
    FetchContent_MakeAvailable(Catch2)
    list(APPEND CMAKE_MODULE_PATH ${catch2_SOURCE_DIR}/contrib)
endif ()

macro (add_catch_test name)
    # XXX not really needed? CK include(CTest)
    target_link_libraries(${name} Catch2::Catch2)
    include(Catch)
    catch_discover_tests(${name})
endmacro ()

add_subdirectory(message)
add_subdirectory(context_ops)
add_subdirectory(socket_ops)
add_subdirectory(socket)
add_subdirectory(signal)
add_subdirectory(actor)
add_subdirectory(cpp20/socket)
